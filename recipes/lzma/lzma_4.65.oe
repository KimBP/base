require lzma.inc

COMPATIBLE_HOST_ARCHS = ".*linux .*darwin"

SRC_URI += "\
	file://001-large_files.patch \
	file://002-lzmp.patch \
	file://003-compile_fixes.patch \
	file://100-static_library.patch \
"

do_patch[prefuncs] += "do_patch_crlf"
do_patch_crlf() {
	# Replace MS-DOS line-endings with Unix style line-endings
	find ${S} -type f -print0 | xargs -0 sed 's/\r$//' -i
}

do_compile() {
	oe_runmake -C C/LzmaUtil \
		CC='${CC}' CFLAGS='${CFLAGS}' \
		AR='${AR}' RANLIB='${RANLIB}'
	oe_runmake -C CPP/7zip/Compress/LZMA_Alone \
		CXX='${CXX} ${CXXFLAGS}' CXX_C='${CC} ${CFLAGS}'
}

do_install() {
	install -d ${D}${bindir} ${D}${libdir}
	install -m 0755 CPP/7zip/Compress/LZMA_Alone/lzma ${D}${bindir}
	install -m 0644 C/LzmaUtil/liblzma.a ${D}${libdir}
}
